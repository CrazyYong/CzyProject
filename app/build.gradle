apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "com.czy.admin.czyproject"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


//创建一个名字为hello的 task
task hello {

    //创建一个 Action , 添加到 Action 列表的头部
    doFirst(new Action<Task>() {
        @Override
        void execute(Task task) {
            println "action1++++++++++"
        }
    })

    //创建一个 Action , 添加到 Action 列表的头部
    doFirst {
        println "action2++++++++++"
    }

}

//创建Task的另外一种方式
tasks.create("hello2"){
    doFirst{
        println "hello2+++++"
    }
}

//自定义DefaultTask子类
class MyTask extends DefaultTask{

    @TaskAction
    void action(){
        println "action1+++++"
    }
}

//创建hello3 task
task hello3(type:MyTask){
    doLast{
        println "action2+++++++"
    }
}

//八、Task依赖
//1、dependsOn
task task1 << {
    println "我是task1+++++++"
}
task task2 << {
    println "我是task2+++++++"
}
//task2 依赖 task1, 执行task2之前先执行task1
task2.dependsOn task1

//2、mustRunAfter
task task3 << {
    println "我是task3----"
}

task task4 << {
    println "我是task4----"
}

//task4 运行之前先运行task3
task4.mustRunAfter task3

//3、shouldRunAfter 形成依赖关系可有可无
task task5 << {
    println "我是task5----"
}

task task6 << {
    println "我是task6----"
}
//在出现语法矛盾的情况下，依赖关系形成闭环，会自动打破闭环。不会报错
task5.shouldRunAfter task6


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okio:okio:1.5.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'ru.bartwell:exfilepicker:2.1'
}
